buildscript {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    dependencies {
        classpath "com.jakewharton:butterknife-gradle-plugin:8.4.0"
    }
}
if (isModule.toBoolean()) {
    apply plugin: 'com.android.library'
    apply plugin: 'com.jakewharton.butterknife'
} else {
    apply plugin: 'com.android.application'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        if (!isModule.toBoolean()) {
            applicationId "com.example.team.comearnapp"
        }
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"


        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }
    //这一段的目的是强制让所有模块都用相同的支持库版本。
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '26.1.0'
                }
            }
        }
    }
    //这一段的目的是强制让所有模块都用相同的支持库版本。


    sourceSets {
        main {
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':commonlibrary')
    implementation project(':comearnlib')


    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    annotationProcessor 'com.github.mzule.activityrouter:compiler:1.1.7'
    implementation 'com.github.bumptech.glide:glide:4.0.0'
    implementation 'com.android.support:recyclerview-v7:27.1.0'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    implementation 'com.squareup.okhttp:okhttp:2.7.2'
    implementation 'com.squareup.retrofit2:retrofit:2.1.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.3.0'
    annotationProcessor rootProject.ext.router_compiler
}
